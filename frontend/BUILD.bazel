load("@aspect_rules_esbuild//esbuild:defs.bzl", "esbuild")
load("@aspect_rules_js//js:defs.bzl", "js_run_devserver")
load("@aspect_rules_swc//swc:defs.bzl", "swc")
load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@bazel_skylib//lib:partial.bzl", "partial")
load("@npm//:@hey-api/openapi-ts/package_json.bzl", openapi_ts = "bin")
load("@npm//:http-server/package_json.bzl", http_server = "bin")
load("@npm//:live-server/package_json.bzl", live_server = "bin")
load("@rules_itest//:itest.bzl", "itest_service")

ts_project(
    name = "frontend",
    srcs = ["frontend.tsx"],
    allow_js = True,
    data = [
        "//:node_modules/react",
        "//:node_modules/react-dom",
    ],
    declaration = True,
    no_emit = True,
    resolve_json_module = True,
    source_map = True,
    transpiler = partial.make(
        swc,
        swcrc = "//:.swcrc",
    ),
    tsconfig = "//:tsconfig",
    deps = [
        ":apiclient",
        "//:node_modules/@types/react",
        "//:node_modules/@types/react-dom",
        "//:node_modules/react",
        "//:node_modules/react-dom",
    ],
)

esbuild(
    name = "bundle",
    srcs = [
        "index.html",
        ":frontend",
    ],
    define = select({
        "//:compilation_mode_opt": {"process.env.NODE_ENV": '"production"'},
        "//conditions:default": {},
    }),
    entry_point = "frontend.tsx",
    output_dir = True,
    sourcemap = "linked",
    splitting = True,
    tsconfig = "//:tsconfig",
    visibility = ["//visibility:public"],
)

http_server.http_server_binary(
    name = "http_server",
)

js_run_devserver(
    name = "devserver",
    args = [
        package_name(),
        "-o",
        "--enable-source-maps",
    ],
    data = [
        "index.html",
        ":bundle",
    ],
    tool = ":http_server",
)

openapi_ts.openapi_ts(
    name = "gen_apiclient",
    srcs = ["//api"],
    args = [
        "--debug",
        "--input",
        # Need to prefix with local path to tell the generator this is a path, not an API repository object.
        # https://heyapi.dev/openapi-ts/configuration/input
        "./$(rootpath //api)",
        "--output",
        package_name() + "/apiclient",
    ],
    log_level = "debug",
    out_dirs = ["apiclient"],
)

ts_project(
    name = "apiclient",
    srcs = [
        "apiclient.ts",
        ":gen_apiclient",
    ],
    allow_js = True,
    declaration = True,
    no_emit = True,
    resolve_json_module = True,
    source_map = True,
    transpiler = partial.make(
        swc,
        swcrc = "//:.swcrc",
    ),
    tsconfig = "//:tsconfig",
)

live_server.live_server_binary(
    name = "live_server",
)

itest_service(
    name = "sut",
    args = [
        package_name(),
        "--verbose",
        "--port=8000",
    ],
    data = [
        "index.html",
        ":bundle",
    ],
    exe = ":live_server",
    deps = ["//server:sut"],
)
